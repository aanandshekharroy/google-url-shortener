apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
android {
    compileSdkVersion 27
    defaultConfig {
        applicationId "com.example.theseus.urlshortener"
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner 'com.github.tmurakami.dexopener.DexOpenerAndroidJUnitRunner'
        buildConfigField "String", "URL_SHORTENER_API", "\""+getAPIKey()+"\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
    lintOptions {
        abortOnError false
    }

}
repositories {
    jcenter()
}

configurations {
    ktlint
}

configurations.all { config ->
    if (config.name.contains('UnitTest')) {
        config.resolutionStrategy.eachDependency { details ->
            if (details.requested.group == 'com.squareup.leakcanary' && details.requested.name == 'leakcanary-android') {
                details.useTarget(group: details.requested.group, name: 'leakcanary-android-no-op', version: details.requested.version)
            }
        }
    }
}
def getAPIKey() {
    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
    return properties.getProperty("URL_SHORTENER_API")
}

//lintOptions {
//    abortOnError false
//}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2-alpha1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    kapt 'com.google.dagger:dagger-compiler:2.14.1'
    implementation 'com.google.dagger:dagger:2.14.1'
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    implementation "org.jetbrains.anko:anko:$anko_version"
    implementation 'com.github.apl-devs:appintro:v4.2.3'

    testImplementation "org.mockito:mockito-core:$mockito_version"
    androidTestCompile ("org.mockito:mockito-android:$mockito_version")
    androidTestCompile 'com.github.tmurakami:dexopener:0.10.1'
    implementation 'com.facebook.stetho:stetho:1.5.0'

    testImplementation "org.robolectric:robolectric:3.7.1"

    implementation 'com.google.android.gms:play-services-auth:11.8.0'

    implementation 'com.android.support:design:27.1.0'

    implementation 'com.jakewharton.rxbinding2:rxbinding:2.1.1'

    testImplementation 'pl.pragmatists:JUnitParams:1.1.1'

    implementation "org.jetbrains.anko:anko-design:$anko_version"

    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'io.reactivex.rxjava2:rxkotlin:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'com.jakewharton.retrofit:retrofit2-rxjava2-adapter:1.0.0'

    // ktlint
    ktlint "com.github.shyiko:ktlint:0.15.0"

    //Room Database
    implementation "android.arch.persistence.room:runtime:$roomVersion"

    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"
    kapt "android.arch.persistence.room:compiler:$roomVersion"

    androidTestImplementation "android.arch.persistence.room:testing:$roomVersion"
    androidTestImplementation "android.arch.core:core-testing:$arch_core"
    implementation 'com.google.code.gson:gson:2.8.0'

    implementation "android.arch.persistence.room:rxjava2:$roomVersion"

    implementation "com.daimajia.swipelayout:library:1.2.0@aar"

    implementation 'org.greenrobot:eventbus:3.1.1'
}
task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "src/**/*.kt"
    //args "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
}
check.dependsOn ktlint